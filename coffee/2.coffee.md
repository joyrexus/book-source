# First-Class Functions and Applicative Programming

    _ = require 'underscore'
    {deepEqual, ok} = require 'assert'

    eq = deepEqual

---

    stanza = (n) ->
      _.chain([])
       .push(n + " bottles of beer on the wall")
       .push(n + " bottles of beer")
       .push("Take one down, pass it around")
       .push(
         if n > 1
           "#{--n} bottles of beer on the wall."
         else
            "No more bottles of beer on the wall!"
       )
       .value()
       .join('\n')

    ok stanza(2) is
      '''
      2 bottles of beer on the wall
      2 bottles of beer
      Take one down, pass it around
      1 bottles of beer on the wall.
      '''

> Functional programming is about pulling programs apart and reassembling them from the same parts, abstracted behind function boundaries. Thinking in this way, you can imagine that the `stanza` function is the part of the "99 Bottles" program that abstracts lyric generation. Therefore, the part of the program that abstracts the assembly of the verse segments into a song is as follows:

    song = (start, end, stanza) -> 
      iter = (acc, n) -> acc.concat stanza(n)
      [start..end]
        .reduce(iter, [])
        .join('\n')

    ok song(2, 1, stanza) is
      '''
      2 bottles of beer on the wall
      2 bottles of beer
      Take one down, pass it around
      1 bottles of beer on the wall.
      1 bottles of beer on the wall
      1 bottles of beer
      Take one down, pass it around
      No more bottles of beer on the wall!
      '''

---

> In the case of JavaScript, the dynamic nature of the this reference can be exploited to perform a bit of metaprogramming. For example, observe the following constructor function:

    Point2D = (@x, @y) ->

> When used with new, the `Point2D` function gives a new object instance with the proper fields set as you might expect:

    eq new Point2D(0, 1),
      x: 0
      y: 1

> However, the `Function.call` method can be used to metaprogram a derivation
> of the `Point2D` constructor’s behavior for a new Point type:

    Point = (x, y, @z) -> Point2D.call(@, x, y)

    eq new Point(10, -1, 100),
      x: 10
      y: -1
      z: 100

> Nowhere did `Point3D` explicitly set the values for `@x` and `@y`, but by
> dynamically binding the this reference in a call to `Point2D` it became
> possible to change the target of its property creation code.

---

> The three canonical examples of applicative functions are `map`, `reduce`,
> and `filter`. Observe how they operate:

    nums = [1..5]

    double = (nums) ->
      nums.map (n) -> n * 2

    eq double(nums), [2, 4, 6, 8, 10]

    avg = (nums) ->
      sum = nums.reduce((memo, next) -> memo + next)
      sum / nums.length

    evens = (nums) ->
      nums.filter (n) -> not (n % 2)

    eq evens(nums), [2, 4]

---

    peeps = 
      Bill: 27
      Ann: 32

    expected = [ ['Bill', 27], ['Ann', 32] ]

    result = _.map(peeps, (v, k) -> [k, v])
    eq result, expected
      
    result = ([k, v] for k, v of peeps)
    eq result, expected

    all = (args...) -> 
      iter = (bool, f) -> bool and f()
      args.reduceRight(iter, true)

    any = (args...) -> 
      iter = (bool, f) -> bool or f()
      args.reduceRight(iter, false)

    T = -> return true
    F = -> return false

    ok   all T, T, T
    ok ! all T, F, T

    ok   any F, T, F
    ok ! any F, F, F

We can also leverage `Array::every` and `Array::some` for similar effect:

    every = (args...) ->        
      args.every (f) -> f()

    some = (args...) ->        
      args.some (f) -> f()

    ok   every T, T, T
    ok ! every T, F, T

    ok   some F, T, F
    ok ! some F, F, F

---

> The find function is fairly straightforward; it takes a collection and a predicate and returns the first element for which the predicate returns true.

    Array::find = (f) -> 
      return x for x in @ when f(x)

    ok [1, 2, 3].find((x) -> x > 2) is 3
    ok [1, 2, 3].find((x) -> x > 5) is undefined
    ok ['a', 'b', 3, 'd'].find(_.isNumber) is 3

Let's find some primes:

    congruent = (x, y) -> (x % y) is 0

    prime = (x) ->
        m = 2 # modulus
        while m <= Math.sqrt x
          return false if congruent(x, m)
          m += 1
        x > 1

    ok [7910..7920].find(prime) is 7919

> `Array::reject` is essentially the opposite of `Array::filter`; it takes a predicate and returns a collection of values that excludes values for which the predicate returned true. 

    Array::reject = (f) -> 
      x for x in @ when not f(x)

    result = ['a', 'b', 3, 'd'].reject _.isNumber
    eq result, ['a', 'b', 'd']

    result = [1..10].reject(prime)
    eq result, [1, 4, 6, 8, 9, 10]

> The `complement` function takes a predicate and returns a function that
> reverses the sense of the result of said predicate. 

    complement = (pred) ->
      (args...) -> not pred.apply(null, args)

> It can then be used with `Array::filter` to achieve the same effect as 
> `Array::reject`:

    notPrime = complement prime
    result = [1..10].filter(notPrime)
    eq result, [1, 4, 6, 8, 9, 10]
    eq result, [1..10].reject(prime)

                                          # compare with ...
    ok [1, 2, 3, 4].every _.isNumber      # _.all
    ok ['x', 'y', 3].some _.isNumber      # _.any

> The `sortBy` function takes a collection and a function, and returns a sorted collection based on the criteria determined by the passed function. 

    people = [
      name: "Bob"
      age: 40
     ,
      name: "Ann"
      age: 32
     ,
      name: "Bill"
      age: 27
    ]
    
    result = _
      .sortBy(people, (p) -> p.age)
      .map((p) -> "#{p.name} is #{p.age}")

    eq result, [ 'Bill is 27', 'Ann is 32', 'Bob is 40' ]

Alternatively ...

    result = people
      .sort((a, b) -> a.age - b.age)
      .map((p) -> "#{p.name} is #{p.age}")

    eq result, [ 'Bill is 27', 'Ann is 32', 'Bob is 40' ]

> The `groupBy` function takes a collection and a criteria function, and returns
> an object where the keys are the criteria points returned by the function, and
> their associated values are the elements that matched.

    foods = [
      name: 'apple'
      type: 'fruit'
      price: .7
     ,
      name: 'banana'
      type: 'fruit'
      price: .9
     ,
      name: 'tomato'
      type: 'vegetable'
      price: .5
     ,
      name: 'cucumber'
      type: 'vegetable'
      price: .25
    ]

    result = _.groupBy foods, (f) -> f.type

    expected = 
      fruit: [ 
        { name: 'apple',  type: 'fruit', price: 0.7 },
        { name: 'banana', type: 'fruit', price: 0.9 } 
      ]
      vegetable: [
        { name: 'tomato',   type: 'vegetable', price: 0.5  },
        { name: 'cucumber', type: 'vegetable', price: 0.25 }
      ]

    eq result, expected

> The `countBy` function works similarly to `groupBy`, except that it returns an
> object with keys of the match criteria associated with its count.

    result = _.countBy foods, (f) -> f.type

    expected =
      fruit: 2
      vegetable: 2

    eq result, expected


## Defining a Few Applicative Functions

    cat = (args...) ->
      [].concat(args...)

    eq cat([1,2,3], [4,5], [6,7,8]),
       [1..8]

    cons = (head, tail) ->
      cat [head], tail          # or [head].concat tail

    eq cons('x', [1, 2, 3]),
       ['x', 1, 2, 3]

> The function `mapcat` takes a function, `f`, that it uses in the same
manner as `map`, calling it for every element in the given collection. This use
of `f` is the applicative nature of `mapcat`. Additionally, `mapcat`
concatenates all of the elements of the result of `map`:

    mapcat = (f, arr) ->
      cat arr.map(f)...

Alternatively ...

    mapcat = (f, arr) ->
      _.flatten arr.map(f)

    Array::flatten = ->
      iter = (memo, next) -> memo.concat next
      @reduce(iter, [])

    mapcat = (f, arr) ->
      arr.map(f).flatten()

    mapcat = (f, arr) ->
      (f(x) for x in arr).flatten()

    consTo = (x) ->
      (i) -> cons i, [x]

    consToB = consTo('b')

    eq consToB('a'),
       ['a', 'b']

    eq consTo('b')('a'), 
       ['a', 'b']

    eq mapcat(consTo('|'), [1..3]),
       [1, "|", 2, "|", 3, "|"]

The operation of `mapcat` is such that when the mapped function returns an
array, it can be flattened a level. We could then use `mapcat` and another
function, `chomp`, to define a third function, `interpose`:

    Array::chomp = ->
      @.slice(0, -1)

    eq [1, 2, 3].chomp(),
       [1, 2]

    interpose = (infix, arr) ->
      iter = (x) -> consTo(infix)(x)
      mapcat(iter, arr).chomp()

    result = interpose '|', ['a', 'b', 'c']

    eq result, ['a', '|', 'b', '|', 'c']

---

    eq _.keys(peeps),
       [ 'Bill', 'Ann' ]

    eq _.values(peeps),
       [ 27, 32 ]

The `pluck` function takes an array of objects and a string and returns all of
the values at the given key for each object in the array:

    eq _.pluck(people, 'name'),
       [ 'Bill', 'Ann', 'Bob' ] 

Alternatively ...

    pluck = (arr, key) ->
      obj[key] for obj in arr

    eq pluck(people, 'name'),
       [ 'Bill', 'Ann', 'Bob' ] 

    eq pluck(people, 'age'),
       [ 27, 32, 40 ] 

Another way of viewing a JavaScript object is as an array of arrays, each
holding a key and a value. Underscore provides a function named `pairs` that
takes an object and turns it into this nested array:

    eq _.pairs(peeps), 
       [ [ 'Bill', 27 ], [ 'Ann', 32 ] ]

Alternatively ...

    pairs = (obj) -> [k, v] for k, v of obj

    nestedPairs = [ 
      [ 'Bill', 27 ] 
      [ 'Ann' , 32 ] 
    ]
    eq pairs(peeps), nestedPairs

This nested array view can be processed using sequential operations and
reassembled into a new object:

    object = (pairs) ->
      obj = {}
      obj[k] = v for [k, v] in pairs
      obj

    eq object(nestedPairs), peeps
    eq object(pairs(peeps)), peeps

    upcaseKey = (p) -> [p[0].toUpperCase(), p[1]]
    result = pairs(peeps).map(upcaseKey)

    eq result, [ 
      [ 'BILL', 27 ] 
      [ 'ANN' , 32 ] 
    ]

    eq object(pairs(peeps).map(upcaseKey)),
      BILL: 27
      ANN: 32
    
---

> Another common function on maps is to flip the keys and values via the
> `invert` function:

    expected =
      27: 'Bill'
      32: 'Ann'

    eq _.invert(peeps), expected

Alternatively ...

    invert = (obj) ->
      object([v, k] for k, v of obj)

    eq invert(peeps), expected

    people.push name: 'Joe'
    eq people, [ 
      { name: 'Bill', age: 27 },
      { name: 'Ann',  age: 32 },
      { name: 'Bob',  age: 40 },
      { name: 'Joe' } 
    ]

    eq _.pluck(people, 'age'),
       [ 27, 32, 40, undefined ]

> Two functions - `pick` and `omit` — (potentially) filter objects based on their
arguments. 

> The `omit` function takes a blacklist to remove keys from an
object:

    person = 
      name: 'Bill'
      age: 27
      sex: 'M'
      edu: 'PhD'

    eq _.omit(person, 'age', 'edu'),
      name: 'Bill'
      sex: 'M'

Alternatively ...

    omit = (obj, omits...) ->
      q = {}
      q[k] = v for k, v of obj when not (k in omits)
      q

    eq omit(person, 'age', 'edu'),
      name: 'Bill'
      sex: 'M'

> The `pick` function takes a whitelist to take keys:

    eq _.pick(person, 'name', 'age'),
      name: 'Bill'
      age: 27

Alternatively ...

    pick = (obj, includes...) ->
      q = {}
      q[k] = v for k, v of obj when k in includes
      q

    eq pick(person, 'name', 'age'),
      name: 'Bill'
      age: 27

> Finally, we consider the selector functions, useful in finding certain
objects based on keyed criteria: `findWhere` and `where`. 

> The `findWhere` function takes an array of objects and returns the first one that matches the criteria given in the object in the second argument:

    people = [
      name: 'Bob'
      age: 40
      sex: 'M'
     ,
      name: 'Ann'
      age: 32
      sex: 'F'
     ,
      name: 'Bill'
      age: 27
      sex: 'M'
     , 
      name: 'Jim'
      age: 40
      sex: 'M'
    ]

    match =
      age: 40
      sex: 'M'

    eq _.findWhere(people, match),
       name: 'Bob'
       age: 40
       sex: 'M'

Alternatively ...

    Array::findWhere = (match) ->
      for obj in @
        statuses = (obj[k] is v for k, v of match)
        return obj if statuses.every (x) -> x is true

    eq people.findWhere(match),
       name: 'Bob'
       age: 40
       sex: 'M'

> The `where` function operates similarly except that it operates over an array
> and returns *all* of the objects that match the criteria:

    expected = [ 
      { name: 'Bob', age: 40, sex: 'M' },
      { name: 'Jim', age: 40, sex: 'M' } 
    ]

    eq _.where(people, match), expected

Alternatively ...

    Array::where = (match) ->
      @filter (obj) ->
        (obj[k] is v for k, v of match).every (x) -> x

    eq people.where(match), expected


## Tabular Data

> The `select` function uses `pick` on each object in the array to pull out whitelisted keys into new objects, thus preserving the table abstraction:

    select = (objs, keys...) ->
      pick(obj, keys...) for obj in objs

    expected = [ 
      { name: 'Bob', age: 40 },
      { name: 'Ann', age: 32 },
      { name: 'Bill', age: 27 },
      { name: 'Jim', age: 40 } 
    ]

    eq select(people, 'name', 'age'), expected

(Note that our `select` is unlike underscore's own `select` function, which just
serves as an alias for `filter`.)

Alternatively ...

    Array::select = (keys...) ->
      object([k, obj[k]] for k in keys) for obj in @

    eq people.select('name', 'age'), expected

... or ...

    Array::select = (keys...) ->
      pick(obj, keys...) for obj in @

    eq people.select('name', 'age'), expected

> As shown, the `select` function itself returns a table-like data structure,
which can be further processed using `select`:

    eq people.select('name', 'age').select('name'),
       people.select('name')

> Finally, the abstraction can be intentionally broken by purposefully pulling
out only the desired data:

    result = _.pluck people.select('name'), 'name'

    eq result, [ 'Bob', 'Ann', 'Bill', 'Jim' ]

Alternatively ...
    
    result = people
      .select('name')
      .reduce(((memo, next) -> memo.concat next['name']), [])

    eq result, [ 'Bob', 'Ann', 'Bill', 'Jim' ]

... or ...

    # return values of key from each object in array
    Array::valuesFromKey = (key) ->
      iter = (memo, next) -> memo.concat next[key]  # accumulate values
      @select(key).reduce(iter, [])

    eq people.valuesFromKey('name'), [ 'Bob', 'Ann', 'Bill', 'Jim' ]

    rename = (obj, mapping) ->
      remapped = {}
      remapped[mapping[k] or k] = v for k, v of obj
      remapped

Alternatively ...

    rename = (obj, mapping) ->
      key = (k) -> mapping[k] or k
      object([key(k), v] for k, v of obj)

    person = 
      name: 'Bill'
      age: 27
      edu: 'PhD'
      sex: 'M'    # rename `sex` as `gender`

    eq rename(person, sex: 'gender'),
      name: 'Bill'
      age: 27
      edu: 'PhD'
      gender: 'M'

We can extend the `Array` prototype with an `as` method using `rename` to work
against the table abstraction:

    Array::as = (mapping) ->
      @map (obj) -> rename(obj, mapping);

> As you’ll notice, `Array::as` works against the table abstraction by simply
> mapping the `rename` over each of the contained objects.

    expected = [ 
      { name: 'Bob', years: 40 },
      { name: 'Ann', years: 32 },
      { name: 'Bill', years: 27 },
      { name: 'Jim', years: 40 } 
    ]

    eq expected, people
      .select('name', 'age')
      .as(age: 'years')

> Finally, we can square the circle of providing basic SQL capabilities against a
table abstraction by implementing a function akin to SQL’s `WHERE` clause:

    Array::where = (pred) ->
      iter = (memo, next) ->
        if pred(next) then memo else _.without(memo, next)
      @reduce iter, @

> And like the rest of the functions that work against the table abstraction,
> `where` can be chained:

    expected = [ 
      { name: 'Bob', years: 40 },
      { name: 'Ann', years: 32 },
      { name: 'Jim', years: 40 } 
    ]

    overThirty = (p) -> p.age > 30

    eq expected, people
      .select('name', 'age')
      .where(overThirty)
      .as(age: 'years')

    eq expected, people
      .select('name', 'age')
      .as(age: 'years')
      .where((p) -> p.years > 30)


For a further development of these ideas, see [codd](https://github.com/fogus/codd), a functional programming library built on [Lemonad](http://fogus.github.io/lemonad/) to provide relational algebra operations in JavaScript.
