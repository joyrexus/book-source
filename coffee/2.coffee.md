# First-Class Functions and Applicative Programming

    _ = require 'underscore'
    {deepEqual, ok} = require 'assert'

    eq = deepEqual

---

    stanza = (n) ->
      _.chain([])
       .push(n + " bottles of beer on the wall")
       .push(n + " bottles of beer")
       .push("Take one down, pass it around")
       .push(
         if n > 1
           "#{--n} bottles of beer on the wall."
         else
            "No more bottles of beer on the wall!"
       )
       .value()
       .join('\n')

    ok stanza(2) is
      '''
      2 bottles of beer on the wall
      2 bottles of beer
      Take one down, pass it around
      1 bottles of beer on the wall.
      '''

> Functional programming is about pulling programs apart and reassembling them from the same parts, abstracted behind function boundaries. Thinking in this way, you can imagine that the `stanza` function is the part of the "99 Bottles" program that abstracts lyric generation. Therefore, the part of the program that abstracts the assembly of the verse segments into a song is as follows:

    song = (start, end, stanza) -> 
      iter = (acc, n) -> acc.concat stanza(n)
      [start..end]
        .reduce(iter, [])
        .join('\n')

    ok song(2, 1, stanza) is
      '''
      2 bottles of beer on the wall
      2 bottles of beer
      Take one down, pass it around
      1 bottles of beer on the wall.
      1 bottles of beer on the wall
      1 bottles of beer
      Take one down, pass it around
      No more bottles of beer on the wall!
      '''

---

> In the case of JavaScript, the dynamic nature of the this reference can be exploited to perform a bit of metaprogramming. For example, observe the following constructor function:

    Point2D = (@x, @y) ->

> When used with new, the `Point2D` function gives a new object instance with the proper fields set as you might expect:

    eq new Point2D(0, 1),
      x: 0
      y: 1

> However, the `Function.call` method can be used to metaprogram a derivation
> of the `Point2D` constructorâ€™s behavior for a new Point type:

    Point = (x, y, @z) -> Point2D.call(@, x, y)

    eq new Point(10, -1, 100),
      x: 10
      y: -1
      z: 100

> Nowhere did `Point3D` explicitly set the values for `@x` and `@y`, but by
> dynamically binding the this reference in a call to `Point2D` it became
> possible to change the target of its property creation code.

---

> The three canonical examples of applicative functions are `map`, `reduce`,
> and `filter`. Observe how they operate:

    nums = [1..5]

    double = (nums) ->
      nums.map (n) -> n * 2

    eq double(nums), [2, 4, 6, 8, 10]

    avg = (nums) ->
      sum = nums.reduce((memo, next) -> memo + next)
      sum / nums.length

    evens = (nums) ->
      nums.filter (n) -> not (n % 2)

    eq evens(nums), [2, 4]

---

    peeps = 
      Bill: 41
      Ann: 37

    expected = [ ['Bill', 41], ['Ann', 37] ]

    result = _.map(peeps, (v, k) -> [k, v])
    eq result, expected
      
    result = ([k, v] for k, v of peeps)
    eq result, expected

    all = (args...) -> 
      iter = (bool, f) -> bool and f()
      args.reduceRight(iter, true)

    any = (args...) -> 
      iter = (bool, f) -> bool or f()
      args.reduceRight(iter, false)

    T = -> return true
    F = -> return false

    ok   all T, T, T
    ok ! all T, F, T

    ok   any F, T, F
    ok ! any F, F, F

We can also leverage `Array::every` and `Array::some` for similar effect:

    every = (args...) ->        
      args.every (f) -> f()

    some = (args...) ->        
      args.some (f) -> f()

    ok   every T, T, T
    ok ! every T, F, T

    ok   some F, T, F
    ok ! some F, F, F


    ###

    function complement(pred) {
      return function() {
        return !pred.apply(null, _.toArray(arguments));
      };
    }

    function cat() {
      var head = _.first(arguments);
      if (existy(head))
        return head.concat.apply(head, _.rest(arguments));
      else
        return [];
    }

    cat([1,2,3], [4,5], [6,7,8]);
    //=> [1, 2, 3, 4, 5, 6, 7, 8]

    function construct(head, tail) {
      return cat([head], _.toArray(tail));
    }

    construct(42, [1,2,3]);
    //=> [42, 1, 2, 3]

    function mapcat(fun, coll) {
      return cat.apply(null, _.map(coll, fun));
    }

    function butLast(coll) {
      return _.toArray(coll).slice(0, -1);
    }

    function interpose (inter, coll) {
      return butLast(mapcat(function(e) {
        return construct(e, [inter]);
      },
      coll));
    }

    var zombie = {name: "Bub", film: "Day of the Dead"};

    _.keys(zombie);
    //=> ["name", "film"]

    _.values(zombie);
    //=> ["Bub", "Day of the Dead"]

    var library = [{title: "SICP", isbn: "0262010771", ed: 1},
                  {title: "SICP", isbn: "0262510871", ed: 2},
                  {title: "Joy of Clojure", isbn: "1935182641", ed: 1}];

    _.findWhere(library, {title: "SICP", ed: 2});

    //=> {title: "SICP", isbn: "0262510871", ed: 2}

    function project(table, keys) {
      return _.map(table, function(obj) {
        return _.pick.apply(null, construct(obj, keys));
      });
    };

    function rename(obj, newNames) {
      return _.reduce(newNames, function(o, nu, old) {
        if (_.has(obj, old)) {
          o[nu] = obj[old];
          return o;
        }
        else
          return o;
      },
      _.omit.apply(null, construct(obj, _.keys(newNames))));
    };

    function as(table, newNames) {
      return _.map(table, function(obj) {
        return rename(obj, newNames);
      });
    };

    function restrict(table, pred) {
      return _.reduce(table, function(newTable, obj) {
        if (truthy(pred(obj)))
          return newTable;
        else
          return _.without(newTable, obj);
      }, table);
    };

    ###
