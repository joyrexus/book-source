# Higher Order Functions

A higher-order function is a first-class functions that takes a function as an
agrument and returns a function as a result.

---

Underscore and testing utils used below.

    _ = require 'underscore'
    {ok, deepEqual} = require 'assert'
    eq = deepEqual


## Functions That Take Other Functions

Underscore's `max` provides a way to compare arbitrary objects, returning the
maximum value within a collection. It takes an optional second argument, a
function that can be used to generate a numeric value from the object supplied
to it.

    fred = 
      name: 'Fred'
      age: 65
  
    lucy =
      name: 'Lucy'
      age: 36

    people = [fred, lucy]

    eq _.max(people, (p) -> p.age), fred

Note, however, that the comparison between values is always via the
numeric greater-than operator (`>`).

---

Alternatively ...

    oldest = people.reduce (max, next) -> 
      if max.age > next.age then max else next

    eq oldest, fred

---

We can of course define our own `max` function that takes an ordering function
as its first argument.

    max = (isGreater, arr) ->
      arr.reduce (x, y) -> if isGreater(x, y) then x else y

    nums = [1..5]

    isGreater = (x, y) -> x > y
    ok max(isGreater, nums) is 5

    isLess = (x, y) -> x < y
    ok max(isLess, nums) is 1

Alternatively, we can define a `best` function to find “best-fits” in a more flexible manner. It takes two functions: one to build a comparable value, and another to compare two values and return the “best” value of the two.

    best = (value, better, arr) ->
      arr.reduce (x, y) -> 
        X = value(x)
        Y = value(y)
        if X is better(X, Y) then x else y

    ok best(_.identity, Math.max, nums) is 5

    get = (key) ->
      (obj) -> obj and obj[key]

    eq best(get('age'), Math.max, people), fred, 'Fred is oldest at 65'
    eq best(get('age'), Math.min, people), lucy, 'Lucy is yountest at 36'

The implementation of `best` can be tightened up a bit if the supplied `better` function ...

* returns true if the first argument is “better” than the second 
* knows how to “unwrap” its arguments.

For example:

    best = (better, arr) ->
      arr.reduce (x, y) -> 
        if better(x, y) then x else y

    ok best(isGreater, nums) is 5
    ok best(isLess, nums) is 1

    older = (a, b) -> a.age > b.age
    eq best(older, people), fred, 'Fred is oldest at 65'


## More Thinking About Passing Functions

A discussion of how functions can be made more generic and the trade-offs of
doing so.

We start with `repeat`, which takes a number and a value and builds an array
containing some number `n` of the value, duplicated.


    repeat = (n, value) ->
      value for i in [0...n]

    eq repeat(3, 'x'), ['x', 'x', 'x']
    eq repeat(0, 'x'), []
    

### Use functions, not values!

While a function that repeats a value some number of times is good, a function that repeats a computation some number of times is better.

    repeatedly = (n, f) ->
      f(++i) for i in [0...n]       # supply function w/ current repeat count

    roll = -> Math.floor((Math.random() * 6) + 1)   # roll the dice

    rolls = repeatedly(10, roll)                    # a series of dice rolls

    ok rolls.length is 10
    ok(0 < r < 7) for r in rolls

Of course we can still duplicate constant values with `repeatedly`.

    eq repeatedly(3, -> 'x'), ['x', 'x', 'x']

Since `repeatedly` is implemented as a list comprehension iterating over a
range, a number representing the current repeat count is supplied to the function and could be used as you see fit.

    eq repeatedly(3, (i) -> i), [1, 2, 3]

What if we want to call a given function not a specified number of times, but
until a given condition is reached?

    iterTo = (f, check, next) ->
      next = f(next) while check(f(next))

This takes `repeatedly` to the next level in that now even the repeat count is
open-ended and subject to the result of a function call.

Note that the repeating loop is a feed-forward function. In other words, the result
of some call to the passed function is fed into the next call of the function
as its argument.

How would you use `iterateUntil`? A simple use case would be to collect all of
the results of some repeated computation until the value crosses some
threshold. For example, suppose you want to continually double some initial
value (`init`) up to some maximum. For example, let's repeatedly double our
starting value up to 1024.

    double = (n) -> n + n
    test = (n) -> n < 1025

    eq iterTo(double, test, init=1), 
       [2, 4, 8, 16, 32, 64, 128, 256, 512, 1024]

To accomplish the same task with `repeatedly` requires that you know, before
calling, the number of times you need to call your function to generate the
correct array:

    eq repeatedly(10, (exp) -> Math.pow(2, exp)),
       [2, 4, 8, 16, 32, 64, 128, 256, 512, 1024]


## Functions That Return Other Functions

A function returning a constant can actually be quite useful.

    K = (x) -> 
      -> x

Our `K` function captures a single value (or reference) and repeatedly
returns the same value. 

    hi = K('hi')

    ok hi() is 'hi'

Note how a captured function is always the same across invocations.

    f = K(->)
    ok f() is f()

    g = K(K)
    ok g() is g()

Also note how each new closure captures a unique value/reference.

    ok f() isnt g()
    ok K(K) isnt K(K)
    ok K(->) isnt K(->)

Any function created with `->` will return a unique instance, regardless of
the contents of its body.

We can now use `K` in lieu of anonymous functions.

    eq repeatedly(3, -> 'x'), ['x', 'x', 'x']
    eq repeatedly(3, K('x')), ['x', 'x', 'x']

---

Our `invoker` function takes a method and returns a function that will invoke 
that method on any object given. 

    fail = (msg) -> throw new Error msg
    callIf = (cond, f) -> f() if cond

    invoker = (method) ->
      (obj, args...) ->
        fail 'Must provide a target object' unless obj?
        _method = obj[method.name]
        kosher = _method? and method is _method
        callIf kosher, -> obj[method.name](args...)

    reversor = invoker(Array::reverse)

    numberLists = [
      [1, 2, 3]
      [4, 5, 6]
    ]
    
    expected = [
      [3, 2, 1]
      [6, 5, 4]
    ]
    
    eq numberLists.map(reversor), expected

Note that `invoker` returns undefined when an object does not have a given
method.

    input = ['oh noes!', [1, 2, 3]]
    expected = [undefined, [3, 2, 1]]
    eq input.map(reversor), expected

    upper = invoker(String::toUpperCase)
    expected = ['OH NOES!', undefined]
    eq input.map(upper), expected


## Capturing Variables for Great Good

Suppose we need for a function that generates unique strings.

    uniqString = (size) ->
      Math
        .random()           # generate a random number between 0 and 1
        .toString(36)       # convert to base 36
        .substr(2, size)    # slice from index 2 to 2+size

    ok uniqString(10).length is 10

What if the function needed to generate unique strings with a certain prefix?

    uniqString = (prefix) ->
      [prefix, new Date().getTime()].join('')

    result = uniqString('prefix')

    ok /^prefix/.test(result)
    ok result.length is 19

What if we need to return a prefixed string with an increasing suffix starting at some known value?  We need a closure to capture some increasing value, used as the suffix.

    uniqStringFactory = (count) ->
      (prefix) -> prefix + count++

    uniqString = uniqStringFactory(init=0)
    ok uniqString('x') is 'x0'
    ok uniqString('x') is 'x1'

    uniq = uniqStringFactory(init=0)
    ok uniq('x') is 'x0'
    ok uniq('x') is 'x1'

Try to avoid exposing your `count` variable.  This will work ...

    uniq =
      count: 0
      string: (prefix) -> prefix + @count++

    ok uniq.string('x') is 'x0'
    ok uniq.string('y') is 'y1'
    ok uniq.string('z') is 'z2'

... but the `count` variable is exposed:

    uniq.count = 20
    ok uniq.string('x') is 'x20'
    ok uniq.string.call(count: 30, 'x') is 'x30'

Better to hide the counter:

    uniqFactory = (count) ->
      string: (prefix) -> prefix + count++

    uniq = uniqFactory(init=5)
    ok uniq.string('x') is 'x5'

We say that a function is **referentially transparent** when its return value is
only dependent on the arguments supplied to the function.  Note that closures,
then, do not exhibit referential transparency.


## A Function to Guard Against Nonexistence: `fnull`

We're now going to create a function called `fnull`. It guards against
unexpected nulls, and lets us define argument defaults.

Demonstrating an error condition that `fnull` is meant to solve:

    nums = [1, 2, 3, null]

    result = nums.reduce (total, n) -> total * n

    ok result is 0  # 1 * 2 * 3 * null

We use `fnull` to guard functions against null and undefined arguments,
replacing them with some default argument.  `fnull` returns a function that
calls the supplied function argument with a kosher set of arguments, viz. where
any null or undefined args that are given to it are replaced with a default
value.

    fnull = (f, defaultValue) ->
      (args...) ->
        kosher = (e or defaultValue for e, i in args) # use default if null
        f(kosher...)

    iter = (total, n) -> total * n    # our reduce function
    toProduct = fnull(iter, 1)        # replace nulls with 1

Using `fnull` to create `toProduct` protects the product being calculated from
receiving a `null` or `undefined` and thereby messing up the result. 

    ok nums.reduce(toProduct) is 6
    ok [null, null].reduce(toProduct) is 1

Now suppose we have a function that takes a configuration object as input to perform some action.

    total = (order) -> 
      price = order.price
      taxes = price * order.tax
      price + taxes

    order =
      tax: .10
      price: 100

    ok total(order) is 110

Note that `total` will return `NaN` (not a number) if either its `cost`
or `tax` attributes are missing.

    order =
      price: 100

    ok isNaN total(order)

What if we want to encapsulate some defaults values for orders lacking either their `cost` or `tax` attributes?

First, note how we can use `fnull` to produce an identity function that returns
any non-null values it receives as an argument, but returns some default value
if it receives a null value as argument.

    value = fnull(_.identity, 'z')

    ok value('x') is 'x'
    ok value('y') is 'y'
    ok value('')  is 'z'                  # returns default value `z`

We use such an identity function in the following `config` function, which
takes a set of defaults and returns a default savvy "getter" function.

    config = (defaults) ->
      (obj, key) ->
        value = fnull(_.identity, defaults[key])
        obj and value(obj[key])

    total = (order) ->
      defaults =
        tax:  .10
        price: 20
      get = config(defaults)
      price = get(order, 'price')         # returns 20 if unspecified
      taxes = price * get(order, 'tax')
      price + taxes

    order = 
      price: 50
      # tax unspecified, use default!

    ok total(order) is 55

    order = 
      tax: .20
      price: null                         # unspecified!

    ok total(order) is 24                 # since default price is 20


## Object Validators

We often need to validate the veracity of an object based on arbitrary criteria. 
Let's now consider a function for building object-field validating functions.

The following `validator` function takes a number of conditions (predicate
functions returning true or false) and returns a validation function. The
returned validation function executes each predicate on a given object, and it
adds a special error string to an array for each false condition.

If all of the conditions hold, the final result is an empty array; otherwise
the array contains error messages for each false condition.

In other words, `validator` will serve as a constraint-checking utility.

    validator = (conds...) ->
      (obj) ->
        conds.reduce (errs, cond) ->
          if cond(obj)
            errs
          else
            errs.push cond.error
            errs
        , []

Let's first use the validator with a condition that never fails.

    cond = K(true)                            # always returns true
    cond.error = 'NEVER'

    ok cond('') is true
    ok cond({}) is true

    validate = validator(cond)                # always passes
    eq validate(''), []
    eq validate({}), []

Now how about a condition that always returns false?

    cond = K(false)                          # always returns false
    cond.error = 'FAIL!'

    ok cond() is false

    validate = validator(cond)               # always fails
    eq validate({}), ['FAIL!']
    eq validate(''), ['FAIL!']

The `cond` function generates a safer conditional test for passing to the validator.

    cond = (msg, f) ->
      test = (args...) -> f(args...)
      test['error'] = msg
      test

Here we use `validator` to create a validation function that checks for
objects/maps:

    isMap = (obj) -> _.isObject(obj)

    validate = validator(
      cond('should be a map!', isMap)
    )
    eq validate({}), []
    eq validate(''), ['should be a map!']

Again, let's create a validation function with conditions that always return true, so we'll never get any error messages returned:

    validate = validator(
      cond("you'll never see me", K(true)),   # always return true
      cond("you'll never see me", K(true))
    ) 
    eq validate({}), []
    eq validate(''), []

And how about that condition that always returns false?

    fails = cond('FAIL!', K(false))
    ok fails() is false

    validate = validator(fails)               # always fails
    eq validate({}), ['FAIL!']
    eq validate(''), ['FAIL!']

How might we use `validator` to ensure that an object has values associated
with certain keys?  We're aiming to use validator in the following way:

```coffeescript

  validate = validator hasKeys('type', 'color')

  apple = 
    type: 'gala'
    color: 'green'

  validate(apple)

```

Let's first define our `hasKeys` condition:

    hasKeys = (keys...) ->
      test = (obj) -> keys.every (k) -> obj.hasOwnProperty(k)
      keyList = keys.join ', '
      test.error = 'Please specify all keys: ' + keyList
      test

Now we can pass our new condition to `validator`:

    validate = validator hasKeys('type', 'color')

    apple = 
      type: 'gala'
      color: 'green'

    eq validate(apple), []

    orange = 
      type: 'cara cara'

    eq validate(orange), ['Please specify all keys: type, color']

Of course we can pass `hasKeys` as part of multiple conditions:

    validate = validator(
      cond('should be a map!', isMap),
      hasKeys('type', 'color')
    )

    eq validate(apple),  []
    eq validate(orange), ['Please specify all keys: type, color']
