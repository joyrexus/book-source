    _ = require 'underscore'
    {ok, throws, deepEqual} = require 'assert'

> Using `apply`, I can create a neat little function named `splat` 
> that just takes a function and returns another function that takes 
> an array and calls the original with `apply`, so that its elements 
> serve as its arguments.

    splat = (f) ->
      (args) -> f.apply(null, args)

    sum = splat (x, y) -> x + y
    ok sum([1, 2]) is 3

My variant:
    
    splat = (f) ->
      (args) -> f args...

    sum = splat (x, y, z) -> x + y + z
    ok sum([1, 2, 3]) is 6

Getting off track:

    Array::sum = -> @reduce ((a, b) -> a + b), 0

    ok [1, 2, 3].sum() is 6

---

> We can create a function `unsplat` that works opposite from `splat`, 
> taking a function and returning another function that takes any number 
> of arguments and calls the original with an array of the values given:

    unsplat = (f) ->
      -> 
        args = _.toArray(arguments)   # Array.prototype.slice.call(arguments)
        f.call(null, args)

    join = unsplat (array) -> array.join ' '

    ok join(1, 2) is '1 2'
    ok join('a', 'b', 'c') is 'a b c'

My variant:

    unsplat = (f) ->
      (args...) -> f args

    join = unsplat (array) -> array.join ' '

    ok join(1, 2) is '1 2'
    ok join('a', 'b', 'c') is 'a b c'

---

    fail = (msg) -> throw new Error msg
    warn = (msg) -> console.log "warning: #{msg}"
    note = (msg) -> console.log "note: #{msg}"

    isIndexed = (x) -> _.isArray(x) or _.isString(x)

> The function `isIndexed` is also a function providing an abstraction 
> over checking if a piece of data is a string or an array. Building 
> abstraction on abstraction leads to the following complete 
> implementation of `nth`:

    nth = (arr, i) ->
      fail('index should be an integer') if not _.isNumber(i)
      fail('not supported on non-indexed types') if not isIndexed(arr)
      fail('index value out of bounds') if i not in [0..arr.length]
      arr[i]

    ok nth('abc', 0) is 'a'

    nums = [1..3]
    throws (-> nth(nums, 6)), /out of bounds/
    throws (-> nth(1000, 1)), /not supported/
    throws (-> nth('abc', 'i')), /index should be an int/

---

    second = (arr) -> nth(arr, 1)
    ok second('ab') is 'b'
    throws (-> second(1000)), /not supported/

---

    numerically = (x, y) ->
      status =  0  #  x = y
      status = -1  if x < y
      status =  1  if x > y
      status

    numeric = (x, y) -> x >= y

    unsorted = [2, 3, -1, -6, 0, -108, 42, 10]
    expected = [-108, -6, -1, 0, 2, 3, 10, 42]

    sorted = unsorted.sort numerically
    deepEqual sorted, expected

    sorted = unsorted.sort numeric
    deepEqual sorted, expected

    comparator = (pred) ->
      (x, y) ->
        status =  0  #  x = y
        status = -1  if pred(x, y)
        status =  1  if not pred(x, y)
        status

    sorted = unsorted.sort comparator((x, y) -> x <= y)
    deepEqual sorted, expected

---

    ###

    function lameCSV(str) {
      return _.reduce(str.split("\n"), function(table, row) {
        table.push(_.map(row.split(","), function(c) { return c.trim()}));
        return table;
      }, []);
    };

    var peopleTable = lameCSV("name,age,hair\nMerble,35,red\nBob,64,blonde");

    peopleTable;
    # => [["name",  "age",  "hair"],
    #     ["Merble", "35",  "red"],
    #     ["Bob",    "64",  "blonde"]]

    function selectNames(table) {
      return _.rest(_.map(table, _.first));
    }

    function selectAges(table) {
      return _.rest(_.map(table, second));
    }

    function selectHairColor(table) {
      return _.rest(_.map(table, function(row) {
        return nth(row, 2);
      }));
    }

    var mergeResults = _.zip;

    function existy(x) { return x != null };

    function truthy(x) { return (x !== false) && existy(x) };

    function doWhen(cond, action) {
      if(truthy(cond))
        return action();
      else
        return undefined;
    }

    function executeIfHasField(target, name) {
      return doWhen(existy(target[name]), function() {
        var result = _.result(target, name);
        console.log(['The result is', result].join(' '));
        return result;
      });
    }

    ###

